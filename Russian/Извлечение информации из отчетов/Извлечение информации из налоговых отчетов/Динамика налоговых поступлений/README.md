### Описание
Правило ищет изменения налоговых поступлений.

### Примеры найденных сущностей
* **налог на имущество организаций поступил** в бюджет Тульской области **с ростом на 10 млн. рублей**
* поступления по **водному налогу уменьшились на 4%**

### Инструкция
* Создать новый проект в платформе [PolyAnalyst](https://www.megaputer.ru/produkti/)
* Загрузить в проект файл [**Данные_Динамика_налога.txt**](Данные_Динамика_налога.txt)
* Импортировать словарь [**WordClasses_Динамика.pad7**](WordClasses_Динамика.pad7) в **Менеджере словарей**
* Добавить узел **Извлечение сущностей**, в настройках узла:
	 * отключить стандартные сущности
	 * создать новую пользовательскую сущность
	 * открыть **Редактор правил** новой сущности и скопировать в него правило [**Налог.txt**](../Налог/Налог.txt)
	 * выполнить узел
* Добавить еще один узел **Извлечение сущностей**, в настройках узла:
	 * на вкладке **Словари** подключить словарь **WordClasses Динамика**
	 * на вкладке **Сущности** отключить стандартные сущности
	 * создать новую пользовательскую сущность
	 * открыть **Редактор правил** новой сущности и скопировать в него правило [**Динамика_налога.txt**](Динамика_налога.txt):
	 ```  
	rule: динамика_налога
	{
		//Ищем все упоминания налога	
		query: {entity(Налог)}:nalog
		
		//Ищем упоминания сумм или процентов
		rule: размер_изменений
		{
			query: {entity(Currencies)}:sum
				or
			       {phrase({number()}:percent_number, "%" or процент)}:percent

			//Далее ищем контексты, указывающие на изменения в налоговых сборах
			
			//Пример:
			//Поступления по налогу на имущество организаций составили 21,0 млн. рублей, что превышает поступления налога за аналогичный период прошлого года на 11,7 процента или на 2,2 млн. рублей
			rule: изменение_поступлений
			{
			 query: {sentence($nalog, 							//Поступления по НАЛОГУ НА ИМУЩЕСТВО ОРГАНИЗАЦИЙ
							  
					  phrase({term(изменение_поступлений)}:up_down, поступление), 	//ПРЕВЫШАЕТ ПОСТУПЛЕНИЯ налога за аналогичный период прошлого года

					  phrase(на, $percent),						//НА 11,7 ПРОЦЕНТА

					  optional(phrase(на|или, $sum))				//или НА 2,2 МЛН. РУБЛЕЙ

							
							)}:dyn
							
					result: Результат = $dyn
						attribute: Налог = toentity(Налог, $nalog)
						attribute: Изменение = title(norm($up_down))
						attribute: Процент = num($percent_number)
						attribute: Сумма = toentity(Currencies, $sum, field:=Amount)

			}
			
			//Пример:
		   	//поступления по земельному налогу уменьшились на 8,8%, или на 227 639,8 тыс. рублей в абсолютном выражении	
			rule: поступления_изменились
			{
			 query: {phrase(6, $nalog,									//поступления по ЗЕМЕЛЬНОМУ НАЛОГУ
			 
					{снизилось or повысилось or увеличилось or уменьшилось}:up_down,		//УМЕНЬШИЛИСЬ

					repeat(1,2,phrase(optional(или), на, $percent)					//НА 8,8% 
							   or
							   phrase(0,optional(или), на, $sum), allow_punct:=yes)		//ИЛИ НА 227 639,8 ТЫС. РУБЛЕЙ 
					)}:dyn
							
					result: Результат = $dyn
						attribute: Налог = toentity(Налог, $nalog)
						attribute: Процент = todouble($percent_number)
						attribute: Изменение = title(norm($up_down))
						attribute: Сумма = toentity(Currencies, $sum, field:=Amount)
			}
			
			//Пример:
			//Налог на имущество физических лиц поступил в сумме 317,1 млн рублей с ростом к аналогичному периоду 2019 года на 121,1 млн рублей		
			rule: поступил_с_изменением
			{
			 query: {sfollow($nalog, 						//НАЛОГ НА ИМУЩЕСТВО ФИЗИЧЕСКИХ ЛИЦ 
							
					поступил, с or со,					//ПОСТУПИЛ в сумме 317,1 млн рублей С

					{рост or снижение or больше or меньше}:up_down,		//РОСТОМ к аналогичному периоду 2019 года

					phrase(на, $sum),					//НА 121,1 МЛН РУБЛЕЙ

					optional(phrase(на, $percent))				//опциональный аргумент
					)}:dyn

					result: Результат = $dyn
						attribute: Налог = toentity(Налог, $nalog)
						attribute: Процент = todouble($percent_number)
						attribute: Изменение = title(norm($up_down))
						attribute: Сумма = toentity(Currencies, $sum, field:=Amount)
			}
		}
	}
	```
	ИЛИ
	 * импортировать файл [**Динамика_налога.xml**](Динамика_налога.xml), в котором дополнительно настроена постобработка (замена синонимов в колонке **Изменение**)
	 * на вкладке **Словари** подключить словарь [**Synonyms_Динамика_налога.pad7**](Synonyms_Динамика_налога.pad7), предварительно импортировав его в **Менеджере словарей**
	 * выполнить узел

### Структура сущности
* **Результат** - общий вывод найденной сущности
* **Налог** - название налога
* **Изменение** - характер динамики налоговых поступлений ("Рост" или "Снижение")
* **Процент** - процент изменений налоговых поступлений по сравнению с аналогичным периодом прошлого года
* **Сумма** - сумма изменений налоговых поступлений (в числовом выражении) по сравнению с аналогичным периодом прошлого года

### Отчет узла
Отчет узла **Извлечение сущностей** *без применения постобработки* выглядит следующим образом:

| Результат | Налог| Изменение| Процент | Сумма| 
| ------ | ------ | ------ | ------ | ------ | 
| транспортному налогу превышает поступления на 8,2 процента на 3,7 млн. рублей | Транспортный налог | Превышать | 8,2 | 3 700 000,00 |

Отчет узла **Извлечение сущностей** *с применением постобработки* выглядит следующим образом:

| Результат | Налог| Изменение| Процент | Сумма| 
| ------ | ------ | ------ | ------ | ------ | 
| транспортному налогу превышает поступления на 8,2 процента на 3,7 млн. рублей | Транспортный налог | Рост | 8,2 | 3 700 000,00 |

В тексте подсвечиваются следующие фрагменты:
> Поступления по **транспортному налогу превышают поступления** за прошлый год **на 8,2 процента** или на **3,7 млн. рублей**.

