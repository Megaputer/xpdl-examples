rule: сумма_налог

{	// Поиск контекстов, в которых содержится пользовательская сущность Налог (упоминение налогов) или стандартная сущность Currencies (суммы в валютах). 
	query: {entity(Налог)}:налог 
			or
        	   {entity(Currencies)}:сумма
		   
	   

	rule: налог_поступил_в_сумме
	{
		// Поиск предложений, в которых за именованной группой "налог" следует (на любом расстоянии в пределах предложения) глагол "поступил" и фраза, состоящая из словосочетания "в сумме" или "в размере" и именованной группы "сумма". 
		// Пример:
		// За январь-февраль 2020 года налог на прибыль организаций поступил в консолидированный бюджет Московской области в сумме 3 456 836 тыс. рублей
		query: {sfollow($налог, "поступил", phrase("в сумме" or "в размере",$сумма))}:сбор
		
		
		result: Результат = $сбор // Вывод в колонку "Результат" именованной группы "сбор". 
			attribute: Название налога = toentity(Налог, $налог, field:=Налог) // Вывод атрибута "Налог" сущности "Налог" и замена найденного стандартным именем сущности с помощью функции нормализации toentity(). 
			attribute: Сумма сборов = toentity(Currencies, $сумма, field:=Value) // Вывод атрибута "Value" сущности "Currencies" и замена найденного стандартным именем сущности с помощью функции нормализации toentity(). 
	}
	
			
	rule: налог_поступил
	{
		// Поиск предложений, в которых за именованной группой "налог" следует (на любом расстоянии в пределах предложения) фраза, состоящая из глагола "поступило" и именованной группы "сумма". 
		// Пример:
		// Транспортного налога при годовом плане в сумме 1 235 673 тыс. рублей фактически за 1 квартал 2020 года поступило 342 570 тыс. рублей
		query: {sfollow($налог, phrase("поступило", $сумма))}:сбор
		
		result: Результат = $сбор // Вывод в колонку "Результат" именованной группы "сбор". 
			attribute: Название налога = toentity(Налог, $налог, field:=Налог) // Вывод атрибута "Налог" сущности "Налог" и замена найденного стандартным именем сущности с помощью функции нормализации toentity().
			attribute: Сумма сборов = toentity(Currencies,$сумма, field:=Value) // Вывод атрибута "Value" сущности "Currencies" и замена найденного стандартным именем сущности с помощью функции нормализации toentity(). 
	}
	

	rule: поступления_налога
	{
		// Поиск предложений, в которых за словом "поступления" следуют (на любом расстоянии в пределах предложения) именованная группа "налог" и фраза, состоящая из глагола "составили" и именованной группы "сумма". 
		// Пример:
		// Значительный рост наблюдается по поступлению налога на имущество организаций - за 3 месяца 2020 года они составили 347 645 тыс. рублей.	
		query: {sfollow(поступления, $налог, phrase("составили", $сумма))}:сбор
		
		result: Результат = $сбор // Вывод в колонку "Результат" именованной группы "сбор". 
			attribute: Название налога = toentity(Налог, $налог, field:=Налог) // Вывод атрибута "Налог" сущности "Налог" и замена найденного стандартным именем сущности с помощью функции нормализации toentity().
			attribute: Сумма сборов = toentity(Currencies,$сумма, field:=Value) // Вывод атрибута "Value" сущности "Currencies" и замена найденного стандартным именем сущности с помощью функции нормализации toentity(). 
	}
}