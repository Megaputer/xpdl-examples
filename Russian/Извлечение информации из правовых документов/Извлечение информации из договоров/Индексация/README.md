### Описание
Правило ищет информацию об индексации платы, предусмотренной договором.

### Примеры найденных сущностей
* 4.5.1. **Исполнитель вправе в одностороннем порядке не чаще одного раза в год изменить цену, указанную в Приложении № 2 путем осуществления перерасчета текущей цены соответствующего года на индекс потребительских цен (базовая инфляция)**.

### Инструкция
* Создать новый проект в платформе [PolyAnalyst](https://www.megaputer.ru/produkti/)
* Загрузить в проект файл [**Данные_Индексация.txt**](Данные_Индексация.txt)
* Добавить узел **Извлечение сущностей**, в настройках узла:
	* отключить все стандартные сущности, кроме сущности **Organizations** (Организации)
	* выполнить узел
* Добавить еще один узел **Извлечение сущностей**, в настройках узла:
	 * отключить стандартные сущности
	 * создать новую пользовательскую сущность
	 * открыть **Редактор правил** новой сущности и скопировать в него правило [**Индексация.txt**](Индексация.txt):
	 ```
    rule: Inflation 
    {
    	/* Ищем ключевые слова */
    	query: {orn("индекс потребительских цен", инфляция, "инфляционный процесс", "индекс инфляции", "индексация")}:infl
       
    	/* Ищем дополнительную информацию об индексации: период, источник, дату начала */
    	rule: ability
    	{
    		query: {
    				
    					
    				sentence(исполнитель or арендодатель, изменить, 
    				phrase(3, путем, перерасчета, цены, на, $infl) 
    				or 
    				phrase(0, уровень or средний or изменение or увеличение or "в зависимости от" or размер, $infl),
    				{optional(entity(Organizations))}:index,
    				{optional(phrase(0, phrase(0, "не", [чаще], [одного]), раз, в, optional(lemma(numeral)), год or месяц or неделя))}:period,
    				
    				match:=range)
    				
    				
    				OR
    				sentence("арендная плата" or плата or договор or стоимость, 
    				
    				possible(orn(индексироваться, пересматриваться, пересмотр, "может быть изменен", изменен, [изменен], изменение, корректироваться, индексация, phrase(0, {ежегодный}:period, индексация), корректировка)), 
    				
    				phrase(2, уровень or средний or изменение or увеличение or "в зависимости от" or размер or корректировка, $infl),
    				{optional(entity(Organizations))}:index,
    				{optional(
    				orn(
    				phrase(0, phrase(0, "не", [чаще], optional([одного])), раз, в, optional(lemma(numeral)), год or месяц or неделя), 
    							
    				"в каждую годовщину", 
    				phrase(0, за, 12, месяцев),
    				form(ежегодно, ежемесячно, ежедневно, ежегодный),
    				phrase(0, optional("не чаще"), char(num), optional(phrase(0, char(lb), lemma(.), char(rb))), раз, optional(в), год)))}:period,
    			
    				
    				match:=range)
    				
    				}:m
    				
    			result: Результат = $m
    				attribute: Наличие индексации = "Да"
    				attribute: Период индексации = norm($period)
    				attribute: Источник индексов = toentity(Organizations, $index, field:=name)
    	
    	}
    }

	```
	 * выполнить узел

### Структура сущности
* **Результат** - общий вывод найденной сущности
* **Наличие индексации** - наличие информации об индексации в договоре
* **Период индексации** - период, по истечении которого плата может быть проиндексирвоана
* **Источник индексов** - официальный источник, предоставляющий данные об индексах

### Отчет узла
Отчет узла **Извлечение сущностей** выглядит следующим образом:

| Результат| Наличие индексации| Период индексации*| Источник индексов| 
| ------ | ------ |------ |------ |
| По соглашению Сторон, размер твердой суммы арендной платы, предусмотренный п.2.1, настоящего Договора, может быть изменен не ранее, чем по истечении 11 (одиннадцати) месяцев с даты приема передачи помещения, но не чаще одного раза в год и не более чем на размер инфляции за истекший финансовый год в соответствии с официальными данными Федеральной службой государственной статистики. | Да |не чаще 1 раза в 12 месяцев|Федеральная служба государственной статистики|

В тексте подсвечиваются следующие фрагменты:
> 2.3. **По соглашению Сторон, размер твердой суммы арендной платы, предусмотренный п.2.1, настоящего Договора, может быть изменен не ранее, чем по истечении 11 (одиннадцати) месяцев с даты приема передачи помещения, но не чаще одного раза в год и не более чем на размер инфляции за истекший финансовый год в соответствии с официальными данными Федеральной службой государственной статистики.**