rule: сумма
{
	//Фильтрация текстов, где речь идёт о сумме договора
	query: {orn(плата, платеж, стоимость, сумма, phrase(3, размер, плата, match:=range), phrase(3, цена, договор, match:=range))}:сумма
   
	rule: цена
	{
		//Правило, которое ищет ищет сущность рядом с суммой договора, а также период оплаты (если он укзаан)
		query: {phrase(7, {optional(ежемесячный or ежегодный)}:период, $сумма, 
				optional(составлять), {entity(Currencies)}:размер, {optional(orn("в год", "в месяц", "ежемесячно", "ежегодно"))}:период, match:=range)}:цена

		result: Результат = $цена
			attribute: Сумма=toentity(Currencies, $размер)
			attribute: Период=$период
			
		rule: ндс
		{
			//Дополнительное правило, которое ищет, входит ли НДС в указанную сумму
			//Если да, то также ищется сумма НДС в денежном выражении
			query: {phrase(10, $цена, not(orn(entity(Currencies), плата, платеж, стоимость, сумма)), 
					phrase(3, {orn("в т.ч.", "в том числе", "включая", "с учетом")}:с_ндс or {orn("без", "без учета")}:без_ндс, 
					ндс or "налог на добавленную стоимость", 
					optional({entity(Currencies)}:ндс)), match:=range)}:цена_ндс
		
			result: Результат = $цена_ндс
				attribute: Сумма=toentity(Currencies, $размер)
				attribute: Период=$период
				attribute: НДС = if($с_ндс !="", "С учетом", "Без учета")
				attribute: Размер НДС = toentity(Currencies, $ндс)
		} 
	}
}
