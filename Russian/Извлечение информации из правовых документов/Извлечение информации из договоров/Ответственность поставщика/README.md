ъ### Описание
Правило ищет пункт в договоре, определяющий ответственность поставщика за нарушение условий договора.

### Примеры найденных сущностей
* Санкции: **8.2. В случае нарушения сроков поставки Товара Продавец уплачивает Покупателю пени в размере 0.05% (пять сотых процента) стоимости не поставленного в срок Товара за каждый день просрочки, но не более 10% от стоимости Товара, непоставленного своевременно.**

### Инструкция
* Создать новый проект в платформе [PolyAnalyst](https://www.megaputer.ru/produkti/)
* Загрузить в проект файл [**Данные_Ответственность_поставщика.txt**](Данные_Ответственность_поставщика.txt)
* Добавить узел **Извлечение сущностей**, в настройках узла:
	 * отключить стандартные сущности
	 * создать новую пользовательскую сущность
	 * открыть **Редактор правил** новой сущности и скопировать в него правило [**Ответственность_поставщика.txt**](Ответственность_поставщика.txt):
	```
    /* Поиск ключевых слов, связанных с ответственностью поставщика */
    macro: resp() = phrase(4, orn("в случае", "в иных случаях", [при], [за])
    						, orn("несоблюдение", "просрочка", "нарушение", "задержка")
    						, near(3, "поставщик"|"продавец", "поставка"|"оплата"|"договор"|"условия"|"отгрузка"|"отгрузки/вывоза")
    						  or 
    						  phrase(5, "поставщик"|"продавец", "условия настоящего договора, связанные с оплатой")
    						  or 
    						  phrase(2, "вызвать", "действиями/бездействием", "поставщик"|"продавец")
    					   )
    /* Поиск ключевых слов, связанных с размером штрафа */
    macro: size() = phrase(2, уплачивать|оплачивать|phrase(2, "несет ответственность", optional("перед другой стороной"|"перед покупателем"), optional("по договору"), "в виде")
    					   , пени|штраф|"штрафная неустойка"
    					   , "в размере" )
    /* Поиск пункта договора */
    macro: clause() = phrase(length(1,2, char(digit)), char(dot), length(1,2, char(digit)), char(dot))
    				  or phrase(0, length(2, 4, char(num)), char(dot))
    /* Поиск неустойки в процентах */
    macro: percent() = phrase(0, char(num), char("%")|"процент", optional(phrase(0, char(lb), number(), char(rb))))
    				  
    rule: responsibility 
    {
        /* Ищем ключевые слова и пункт договора*/
    	query: follow(2, {macro(clause)}:clause
    					, {macro(resp)}:resp
    				  )
    		
    		/* Ищем размер неустойку поставщика за день и в процентах от общей суммы*/
    		rule: percent_per_day
    		{query: {follow(3, $clause
    						, {sentence(
    									follow($resp, macro(size), {macro(percent)}:percent, "за каждый день просрочки")
    									
    									, {optional(phrase("не более", macro(percent), "от стоимости"|"от суммы", repeat(1,2,lemma(genitive))))}:forfeit
    									, match:=range)}:per_day
    						, match:=range)}:m
    		
    		result: Ответственность поставщика = $m        
    			attribute: Пункт договора «Ответственность поставщика» = $clause
    			attribute: Неустойка поставщика за день = $percent	
    			attribute: Неустойка поставщика = $forfeit				
    				
    		}
    }
	```
	 * выполнить узел

### Структура сущности
* **Ответственность поставщика** - общий вывод найденной сущности
* **Пункт договора «Ответственность поставщика»** - пункт договора, в котором прописана ответственность поставщика
* **Неустойка поставщика за день** - неустойка поставщика за день в процентном выражении
* **Неустойка поставщика** - неустойка поставщика в процентном выражении от общей стоимости товара

### Отчет узла
Отчет узла **Извлечение сущностей** выглядит следующим образом:

| Ответственность поставщика | Пункт договора «Ответственность поставщика» | Неустойка поставщика за день | Неустойка поставщика  |
| ------ | ------ |------ |------ |
| 6.3. При нарушении ПОСТАВЩИКОМ сроков поставки части или всей партии Товара ПОСТАВЩИК уплачивает ПОКУПАТЕЛЮ пени в размере 0,1% от суммы непоставленного Товара за каждый день просрочки. В случае нарушения сроков поставки Товара более чем на 30 (тридцать) календарных дней ПОСТАВЩИК также уплачивает ПОКУПАТЕЛЮ штрафную неустойку в размере 10 % (десяти процентов) от суммы непоставленного Товара. | 6.3. |0,1% | 10 % от суммы непоставленного Товара|

В тексте подсвечиваются следующие фрагменты:
> Ответственность сторон
> **6.3. При нарушении ПОСТАВЩИКОМ сроков поставки части или всей партии Товара ПОСТАВЩИК уплачивает ПОКУПАТЕЛЮ пени в размере 0,1% от суммы непоставленного Товара за каждый день просрочки. В случае нарушения сроков поставки Товара более чем на 30 (тридцать) календарных дней ПОСТАВЩИК также уплачивает ПОКУПАТЕЛЮ штрафную неустойку в размере 10 % (десяти процентов) от суммы непоставленного Товара.**
