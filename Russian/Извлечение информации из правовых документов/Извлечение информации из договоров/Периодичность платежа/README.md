### Описание
Правило ищет упоминания периодичности платежей, а также сроков оплаты, если они указаны.

### Примеры найденных сущностей
* **Оплата** услуг производится **ежемесячно**, **не позднее пятого числа месяца**, следующего за расчетным, согласно выставленным счетам.
* В течение всего срока аренды Арендатор обязуется выплачивать Лизинговой фирме арендную **плату один раз в квартал**.
* Арендатор вносит арендную **плату** Арендодателю 1 в размере 1,200,000 рублей безналичным путем, **не позднее 15 числа каждого месяца**.

### Инструкция
* Создать новый проект в платформе [PolyAnalyst](https://www.megaputer.ru/produkti/)
* Загрузить в проект файл [**Данные_периодичность_платежа.txt**](Данные_периодичность_платежа.txt)
* импортировать словарь синонимов [**Мои_синонимы.pad7**](Мои_синонимы.pad7) в словари проекта
* Добавить узел **Извлечение сущностей**, в настройках узла:
	 * отключить стандартные сущности
	 * на вкладке **Словари** подключить словарь синонимов **Мои_синонимы**
	 * создать новую пользовательскую сущность
	 * открыть **Редактор правил** новой сущности и скопировать в него правило [**Периодичность_платежа.txt**](Периодичность_платежа.txt):
	```
	 rule: платеж
	{
		/*Фильтрация текстов, где речь идёт о платежах.*/
		query: {synonym(плата)}:плата
	   
		rule: периодичность и сроки
		{
			/*Находим периоды и сроки:
			- раз в год;
			- в срок до 24 числа.*/
			query: {orn(ежеквартально, ежемесячно, ежегодно) 
				or	phrase(optional(number(allow_words:=yes)), раз, в, orn(год, месяц, квартал))}:период
				
				or	{phrase({synonym("не позднее")}:ограничение, {number(allow_words:=yes)}:число, числа)}:сроки
			
			rule: периодичный платеж
			{
				/*Правило, которое ищет периодичность платежа: 
				  - Арендная плата вносится Арендатором ежемесячно*/
				query: {phrase(6, $плата, not измениться, $период)}:m

				result: Результат = $m
					attribute: Период = tosynonym($период)
					
					
				rule: срок платежа
				{
					/*Правило, которое дополнительно ищет ограничение по срокам периодичных платежей: 
					  -Уплата лизинговых платежей производится ежемесячно в срок до 24 числа месяца.*/
					query: {phrase($m, $сроки)}:m1
					
					result: Результат = $m1
						attribute: Период = tosynonym($период)
						attribute: Ограничение = concat(tosynonym($ограничение), " ", int($число), " числа")
				}
			}
		
			rule: месячный платёж
			{
				/*Правило, которое ищет ежемесячные платежи: 
				  -Арендатор вносит арендную плату Арендодателю 1 в размере 1,200,000 рублей безналичным путем, не позднее 15 числа каждого месяца.*/
				query: {phrase(10, $плата, phrase($сроки, каждого, месяца))}:m1
					
				result: Результат = $m1
					attribute: Период = "ежемесячно"
					attribute: Ограничение = concat(tosynonym($ограничение), " ", int($число), " числа")
			}	
		}
	}


	```
	 * выполнить узел

### Структура сущности
* **Результат** - общий вывод найденной сущности
* **Период** - периодичность платежа
* **Ограничение** - ограничение по срокам периодичного платежа

### Отчет узла
Отчет узла **Извлечение сущностей** выглядит следующим образом:

| Результат | Период| Ограничение|
| ------ | ------ |------ |
| Оплата ежемесячно не позднее пятого числа |ежемесячно|не позднее 5 числа|
| плату один раз в квартал | ежеквартально ||
| плату не позднее 15 числа каждого месяца | ежемесячно |не позднее 15 числа|

В тексте подсвечиваются следующие фрагменты:
> Уплата лизинговых **платежей** производится **ежемесячно в срок до 24 числа** месяца равными долями от годовой суммы лизинговых платежей, в соответствии с п. 7.1. в безналичном порядке путем перечисления денежных средств на расчетный счет Лизингодателя в соответствии с прилагаемым графиком (Приложение N 2), независимо от фактического использования Имущества.