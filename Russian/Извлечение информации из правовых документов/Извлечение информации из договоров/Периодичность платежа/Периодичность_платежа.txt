rule: платеж
{
	/*Фильтрация текстов, где речь идёт о платежах.*/
	query: {synonym(плата)}:плата
   
	rule: периодичность и сроки
	{
		/*Находим периоды и сроки:
		- раз в год;
		- в срок до 24 числа.*/
		query: {orn(ежеквартально, ежемесячно, ежегодно) 
			or	phrase(optional(number(allow_words:=yes)), раз, в, orn(год, месяц, квартал))}:период
			
			or	{phrase({synonym("не позднее")}:ограничение, {number(allow_words:=yes)}:число, числа)}:сроки
		
		rule: периодичный платеж
		{
			/*Правило, которое ищет периодичность платежа: 
			  - Арендная плата вносится Арендатором ежемесячно*/
			query: {phrase(6, $плата, not измениться, $период)}:m

			result: Результат = $m
				attribute: Период = tosynonym($период)
				
				
			rule: срок платежа
			{
				/*Правило, которое дополнительно ищет ограничение по срокам периодичных платежей: 
				  -Уплата лизинговых платежей производится ежемесячно в срок до 24 числа месяца.*/
				query: {phrase($m, $сроки)}:m1
				
				result: Результат = $m1
					attribute: Период = tosynonym($период)
					attribute: Ограничение = concat(tosynonym($ограничение), " ", int($число), " числа")
			}
		}
	
		rule: месячный платёж
		{
			/*Правило, которое ищет ежемесячные платежи: 
			  -Арендатор вносит арендную плату Арендодателю 1 в размере 1,200,000 рублей безналичным путем, не позднее 15 числа каждого месяца.*/
			query: {phrase(10, $плата, phrase($сроки, каждого, месяца))}:m1
				
			result: Результат = $m1
				attribute: Период = "ежемесячно"
				attribute: Ограничение = concat(tosynonym($ограничение), " ", int($число), " числа")
		}	
	}
}