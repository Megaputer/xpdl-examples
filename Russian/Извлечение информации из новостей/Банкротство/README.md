### Описание
Правило ищет упоминания о банкротстве компаний и организаций.

### Примеры найденных сущностей
* **авиакомпанию «Томск Авиа» признали банкротом** в 2016 году
* **компания Cambridge Analytica обанкротилась** из-за недавнего скандала

### Инструкция
* Создать новый проект в платформе [PolyAnalyst](https://www.megaputer.ru/produkti/)
* Загрузить в проект файл [**Данные_Банкротство.txt**](Данные_Банкротство.txt)
* Добавить узел **Извлечение сущностей**, в настройках узла:
	* отключить все стандартные сущности, кроме сущностей **Companies** (Компании) и  **Organizations** (Организации)
	* выполнить узел
* Добавить еще один узел **Извлечение сущностей**, в настройках узла:
	 * отключить стандартные сущности
	 * создать новую пользовательскую сущность
	 * открыть **Редактор правил** новой сущности и скопировать в него правило [**Банкротство.txt**](Банкротство.txt):
	 ```
	rule: bankruptcy_context
	{
		//Поиск текстов, содержащих ключевые слова и фразы
		query: 
		{банкротство or банкрот or "иск о банкротстве"}:noun
		or
		{банкротиться or обанкротиться}:verb
		
		rule: noun
		{
			//Поиск обанкротившихся компаний и организаций в контексте с ключевыми существительными
			query:{
				//Строительство остановили в 2014 году из-за банкротства строительной компании "Консоль"
				phrase(0, $noun, {entity(Companies|Organizations)}:bankrupt)
				or
				//Авиакомпанию "Томск Авиа" признали банкротом в марте 2016 года
				phrase(0, {entity(Companies|Organizations)}:bankrupt, optional(быть), объявить or признать, $noun)
				 
				 }:m
				  
			rule_except: negative_context
			{
				/*Нужно исключить компании, которые еще не обанкротились. Такие контексты вводятся глаголами "ждать", "ожидать",
				будущим временем ("подадут иск"), а также конструкцией "собираться" + инфинитив*/
				query:
				phrase(7, ждать or ожидать or lemma(future, подать) or "собираться подать", $m)
				
				result: Результат = $m
					attribute: Компания = toentity(Companies|Organizations, $bankrupt, field:=Name)
			}
		}
		
		rule: verb
		{
			//Поиск обанкротившихся компаний и организаций в контексте с ключевыми глаголами
			query:{
				//Cambridge Analytica обанкротилась из-за скандала с данными Facebook
				phrase(0, {entity(Companies|Organizations)}:bankrupt, $verb)
				or
				//Обанкротилась компания Tesla
				phrase(0, $verb, {entity(Companies|Organizations)}:bankrupt)
				  
				  }:m
				  
			result: Результат = $m
					attribute: Компания = toentity(Companies|Organizations, $bankrupt, field:=Name)
		}
	}
	```
	 * выполнить узел

### Структура сущности
* **Результат** - общий вывод найденной сущности
* **Компания** - обанкротвашаяся компания или организация
### Отчет узла
Отчет узла **Извлечение сущностей** выглядит следующим образом:

| Результат| Компания| 
| ------ | ------ |
| банкротство General Motors| General Motors |

В тексте подсвечиваются следующие фрагменты:
> Многие специалисты считают, что **банкротство General Motors** можно было предотвратить.