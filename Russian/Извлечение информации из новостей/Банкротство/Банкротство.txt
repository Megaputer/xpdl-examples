rule: bankruptcy_context
{
	//Поиск текстов, содержащих ключевые слова и фразы
	query: 
	{банкротство or банкрот or "иск о банкротстве"}:noun
	or
	{банкротиться or обанкротиться}:verb
	
	rule: noun
	{
		//Поиск обанкротившихся компаний и организаций в контексте с ключевыми существительными
		query:{
			//Строительство остановили в 2014 году из-за банкротства строительной компании "Консоль"
			phrase(0, $noun, {entity(Companies|Organizations)}:bankrupt)
			or
			//Авиакомпанию "Томск Авиа" признали банкротом в марте 2016 года
			phrase(0, {entity(Companies|Organizations)}:bankrupt, optional(быть), объявить or признать, $noun)
			 
			 }:m
			  
		rule_except: negative_context
		{
			/*Нужно исключить компании, которые еще не обанкротились. Такие контексты вводятся глаголами "ждать", "ожидать",
			будущим временем ("подадут иск"), а также конструкцией "собираться" + инфинитив*/
			query:
			phrase(7, ждать or ожидать or lemma(future, подать) or "собираться подать", $m)
			
			result: Результат = $m
				attribute: Компания = toentity(Companies|Organizations, $bankrupt, field:=Name)
		}
	}
	
	rule: verb
	{
		//Поиск обанкротившихся компаний и организаций в контексте с ключевыми глаголами
		query:{
			//Cambridge Analytica обанкротилась из-за скандала с данными Facebook
			phrase(0, {entity(Companies|Organizations)}:bankrupt, $verb)
			or
			//Обанкротилась компания Tesla
			phrase(0, $verb, {entity(Companies|Organizations)}:bankrupt)
			  
			  }:m
			  
		result: Результат = $m
				attribute: Компания = toentity(Companies|Organizations, $bankrupt, field:=Name)
	}
}