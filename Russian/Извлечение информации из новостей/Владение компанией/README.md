### Описание
Правило извлекает факт владения компанией или организацией физическим лицом.

### Примеры найденных сущностей
* **Александр Филиппов** был **владельцем компании «Автореал»**
* Основной **владелец «Северстали» Алексей Мордашов**

### Инструкция
* Создать новый проект в платформе [PolyAnalyst](https://www.megaputer.ru/produkti/)
* Загрузить в проект файл [**Данные_Владение_компанией.txt**](Данные_Владение_компанией.txt)
* Добавить узел **Извлечение сущностей**, в настройках узла:
	* отключить все стандартные сущности, кроме сущностей  **People** (Люди), **Companies** (Компании) и  **Organizations** (Организации)
	* выполнить узел
* Добавить еще один узел **Извлечение сущностей**, в настройках узла:
	 * отключить стандартные сущности
	 * создать новую пользовательскую сущность
	 * открыть **Редактор правил** новой сущности и скопировать в него правило [**Владение_компанией.txt**](Владение_компанией.txt):
	 ```
	macro: owner() = orn(владелец, совладелец)

	rule: filter_texts 
	{
		//Поиск текстов, где встречаются ключевые компоненты: Люди, Компании и Организации
		query: {entity(People, field:=Name)}:person
		or
		{entity(Companies|Organizations)}:company
	    
		rule: key_words
		//Поиск ключевых слов и фраз с найденными выше ключевыми компонентами
		{
				query: {macro(owner)
				or
				orn(принадлежать, владеть, контролировать)}:owner_np
				and
				($person or $company)
					
				rule_filter: singular
				/*Ограничение поиска только внутри тех текстов, где ключевые слова,
				обозначающие владельца, употреблены в единственном числе*/
				{
					query: $owner_np&lemma(singular)
					
					rule: owner_of
					/*Поиск паттернов:
					- Александр Ильин, совладелец компании "Томский региональный платежный сервис".
					- Фирма "Вуолы-Эко" принадлежит Олегу Лебедю.
					- Андрей Озеров владеет ООО "НФК".*/
					{
						query: phrase(	{$person}:owner,
										optional(orn(быть, стать)),
										optional(orn(назвать, объявить)),
										{$owner_np}:owner,
										{$company}:owner
									)
						or
								phrase(	{$company}:owner,
										{$owner_np}:owner,
										{$person}:owner
									)
										
						or
								phrase(	{$owner_np}:owner,
										{$company}:owner,
										{$person}:owner
									)
									
						result: Результат = concat($owner:person, " ", $owner:owner_np, " ", $owner:company)
							attribute: Владелец = toentity(People, $owner:person, field:=Name)
							attribute: Компания = toentity(Companies|Organizations, $owner:company, field:=Name)
					}
				}	
		}
	}
	```
	 * выполнить узел

### Структура сущности
* **Результат** - общий вывод найденной сущности
* **Владелец** - физическое лицо, владеющее компанией
* **Компания** - полное название компании или организации
### Отчет узла
Отчет узла **Извлечение сущностей** выглядит следующим образом:

| Результат| Владелец| Компания| 
| ------ | ------ |------ |
| Йозеф Фишер-младший владелец компании Fischer| Йозеф Фишер Младший |Fischer International Systems Corp.|

В тексте подсвечиваются следующие фрагменты:
> Ушел из жизни **владелец компании Fischer Йозеф Фишер-младший**.